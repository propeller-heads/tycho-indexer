pragma solidity ^0.8.26;

import {IExecutor} from "@interfaces/IExecutor.sol";
import {
    SafeERC20,
    IERC20
} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {IERC4626} from "@openzeppelin/contracts/interfaces/IERC4626.sol";
import {TransferManager} from "../TransferManager.sol";

error ERC4626Executor__InvalidDataLength();
error ERC4626Executor__InvalidTarget();

contract ERC4626Executor is IExecutor {
    using SafeERC20 for IERC20;

    constructor() {}

    function fundsExpectedAddress(
        bytes calldata /* data */
    )
        external
        view
        returns (address receiver)
    {
        return msg.sender;
    }

    // slither-disable-next-line locked-ether
    function swap(uint256 amountIn, bytes calldata data, address receiver)
        external
        payable
        returns (uint256 amountOut, address tokenOut)
    {
        address target;
        IERC20 tokenIn;

        (tokenIn, target) = _decodeData(data);

        address asset = IERC4626(target).asset();
        bool isRedeem = (address(tokenIn) == target);

        if (isRedeem) {
            tokenOut = asset;
        } else if (address(tokenIn) == asset) {
            tokenOut = target;
        } else {
            revert ERC4626Executor__InvalidTarget();
        }

        // Since there is no way to validate target address,
        // we rely on balance checks to determine the amountOut instead
        // of trusting the amount reported by the target.
        uint256 balanceBefore = IERC20(tokenOut).balanceOf(receiver);

        if (isRedeem) {
            // slither-disable-next-line unused-return
            IERC4626(target).redeem(amountIn, receiver, address(this));
        } else {
            // slither-disable-next-line unused-return
            IERC4626(target).deposit(amountIn, receiver);
        }

        amountOut = IERC20(tokenOut).balanceOf(receiver) - balanceBefore;
    }

    function _decodeData(bytes calldata data)
        internal
        pure
        returns (IERC20 tokenIn, address target)
    {
        if (data.length != 40) {
            revert ERC4626Executor__InvalidDataLength();
        }
        tokenIn = IERC20(address(bytes20(data[0:20])));
        target = address(bytes20(data[20:40]));
    }

    function getTransferData(bytes calldata data)
        external
        payable
        returns (
            TransferManager.TransferType transferType,
            address receiver,
            address tokenIn
        )
    {
        if (data.length != 40) {
            revert ERC4626Executor__InvalidDataLength();
        }
        tokenIn = address(bytes20(data[0:20]));
        receiver = address(bytes20(data[20:40]));
        transferType = TransferManager.TransferType.ProtocolWillDebit;
    }
}
